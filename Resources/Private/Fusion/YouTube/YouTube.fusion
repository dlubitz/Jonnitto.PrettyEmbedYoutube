prototype(Jonnitto.PrettyEmbedYoutube:YouTube) < prototype(Neos.Fusion:Tag) {
    configuration = ${Configuration.setting('Jonnitto.PrettyEmbedYoutube')}
    customPreviewImage = ${q(node).property('customPreviewImage')}
    type = ${q(node).property('type') ? q(node).property('type') : 'video'}
    lightbox = ${q(node).property('lightbox')}
    allowFullScreen = ${q(node).property('allowFullScreen')}
    showinfo = ${q(node).property('showinfo')}
    showControls = ${q(node).property('showControls')}
    loop = ${q(node).property('loop')}
    closedCaptions = ${q(node).property('closedCaptions')}
    showRelated = ${q(node).property('showRelated')}
    maximumWidth = null
    maxWidth = ${this.maximumWidth ? this.maximumWidth : this.configuration.maximumWidth}

    videoID = ${q(node).property('videoID')}
    videoID.@process.getIDFromUrl = Jonnitto.PrettyEmbedYoutube:GetIDFromUrl

    @context.props = ${this}

    @process {
        hasNoVideoID = Jonnitto.PrettyEmbedYoutube:Notification {
            key = 'noVideoID'
            @if.output = ${props.videoID ? false : true}
        }
        isPlaylistANDnoImage = Jonnitto.PrettyEmbedYoutube:Notification {
            key = 'isPlaylistANDnoImage'
            @if.output = ${props.videoID && props.type == 'playlist' && !props.customPreviewImage ? true : false}
        }

        wrapper = Neos.Fusion:Tag {
            @if.render = ${props.configuration.wrapper}
            attributes.class = ${props.configuration.wrapper}
            content = ${value}
        }
    }

    tagName = ${node.context.live ? 'a' : 'div'}

    attributes {
        class = Neos.Fusion:RawArray {
            nodeType = 'jonnitto-prettyembedyoutube'
            lightbox = ${props.lightbox ? props.configuration.lightbox.class : props.configuration.embed.class}
        }
        data-fs = ${props.allowFullScreen ? 'true' : 'false'}
        data-embed = Neos.Fusion:Array {
            src            = ${props.configuration.embed[props.type]}
            videoID        = ${props.videoID}
            var            = ${props.type == 'video' ? '?' : '&'}
            autoplay       = 'autoplay=1'
            showinfo       = ${props.showInfo ? '' : '&showinfo=0'}
            showControls   = ${props.showControls ? '' : '&controls=0'}
            loop           = ${props.loop ? '&loop=1' : ''}
            closedCaptions = ${props.closedCaptions ? '&cc_load_policy=1' : ''}
            showRelated    = ${props.showRelated ? '&rel=1' : '&rel=0'}
        }
        href = ${props.configuration.lightbox[props.type] + props.videoID}
        target = '_blank'
    }

    content = Neos.Fusion:Case {
        hasCustomPreviewImage {
            condition = ${props.customPreviewImage ? true : false}
            renderer = Carbon.Image:Tag {
                attributes.class = 'embed-youtube-costum-preview'
                asset = ${props.customPreviewImage}
                maximumWidth = ${props.maxWidth}
            }
        }
        default {
            condition = true
            renderer = Neos.Fusion:Tag {
                tagName = 'img'
                attributes {
                    class = 'embed-youtube-youtube-preview'
                    src = ${'//img.youtube.com/vi/' + props.videoID + '/' + props.configuration.imgResultion + '.jpg'}
                }
            }
        }
    }


    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @position = 'end 999999999'
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
